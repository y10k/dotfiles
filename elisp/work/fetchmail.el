;;; fetchmail.el --- Emacs から fetchmail を起動する。
;;; $Id$
;;
;; <<< 作者 >>>
;; 土岐 仁謙 <toki@freedom.ne.jp>
;;
;; <<< URL >>>
;; http://www.freedom.ne.jp/toki/elisp/fetchmail.el
;;
;; <<< 使用法 >>>
;; まず fetchmail.el を load-path の通ったディレクトリに置いて .emacs に
;;   (autoload 'fetchmail "fetchmail" nil t)
;; と記述し、fetchmail-server-param-alist 変数の設定をします。
;; このとき注意しないといけないのが query-passwd パラメータの設定で、
;; .fetchmailrc でパスワードの設定をしているときは設定しないか
;; nil 値を設定し、パスワードの設定をしてないときは t を設定します。
;; .fetchmailrc の設定と query-passwd の設定が食い違っていると
;; fetchmail.el は正常に動作しないので、気をつけてください。
;; 後は好みに応じて
;;   fetchmail-default-server
;;   fetchmail-preprocess-hook
;;   fetchmail-postprocess-hook
;;   fetchmail-notify-beep
;;   fetchmail-window
;;   fetchmail-window-height-ratio
;;   fetchmail-window-height-lower-limit
;;   fetchmail-window-height-upper-limit
;; これらの変数の値を適当に設定してください。
;; 面倒ならデフォルト値のままでも構いません。
;; 後は M-x fetchmail を実行すると fetchmail が起動します。
;; query-passwd を t に設定していると、最初に一度だけパスワードを
;; 問い合わせて記憶し、二回目の実行からは記憶したパスワードを使用します。
;; このときパスワードは fetchmail-passwd-alist 変数に記憶され、
;; Emacs Lisp に慣れた人なら簡単に取り出せてしまうので、
;; 端末の前を離れるときは注意してください。
;;

(defvar fetchmail-default-server nil
  "デフォルトのサーバ。")

(defvar fetchmail-server-param-alist '(("localhost" . ()))
  "サーバのパラメータを設定する連想リスト。
例: '((\"mail.freedom.ne.jp\" .
       ((query-passwd . t)
        (protocol . \"pop3\")))
      (\"hepsun2.phys.sci.kobe-u.ac.jp\" .
       ((query-passwd . t)
        (protocol . \"imap\")))
      (\"phys03.phys.sci.kobe-u.ac.jp\" .
       ((query-passwd . t)
        (protocol . \"apop\"))))")

(defvar fetchmail-preprocess-hook ()
  "fetchmail の前処理を登録するフック。")

(defvar fetchmail-postprocess-hook ()
  "fetchmail の後処理を登録するフック。")

(defvar fetchmail-param-func-alist
  '((query-passwd . fetchmail-param-query-passwd)
    (check        . fetchmail-param-check)
    (username     . fetchmail-param-username)
    (protocol     . fetchmail-param-protocol)
    (port         . fetchmail-param-port)
    (timeout      . fetchmail-param-timeout)
    (folder       . fetchmail-param-folder)
    (keep         . fetchmail-param-keep)
    (flush        . fetchmail-param-flush))
  "パラメータのシンボルをキーに持ちパラメータの値を解析する関数のシンボルを
値に持つ連想リスト。fetchmail-server-param-alist のパラメータの解析に
使われる。")

(defvar fetchmail-passwd-alist ()
  "サーバのパスワードを保存する連想リスト。")

(defvar fetchmail-notify-beep t
  "この変数が真のとき fetchmail が終了したことを beep 音で知らせる。")

(defvar fetchmail-window t
  "この変数が真のとき fetchmail バッファをウィンドウで開く。")

(defvar fetchmail-window-height-ratio 0.15
  "fetchmail ウィンドウの高さの割合。")

(defvar fetchmail-window-height-lower-limit 5
  "fetchmail ウィンドウの高さの下限。")

(defvar fetchmail-window-height-upper-limit 10
  "fetchmail ウィンドウの高さの上限。")

(defvar fetchmail-last-server nil
  "最後に使われたサーバの名前が入っている。
fetchmail-start が自動的に設定するので、ユーザが設定してはいけない。")

(defvar fetchmail-process-name "fetchmail"
  "fetchmail プロセスの名前。")

(defvar fetchmail-buffer-name "*fetchmail*"
  "fetchmail バッファの名前。")

(defvar fetchmail-running nil
  "fetchmail が動作中であることを表わすマイナーモード変数")
(if (not (assq 'fetchmail-running minor-mode-alist))
    (setq minor-mode-alist
	  (cons '(fetchmail-running " Fetching mail...")
		minor-mode-alist)))

(defun fetchmail-set-passwd (fetchmail-server passwd)
  "fetchmail-passwd-alist にパスワードを設定する。"
  (let ((pair (assoc fetchmail-server
		     fetchmail-passwd-alist)))
    (if pair
	(setcdr pair passwd)
      (setq pair (list nil))
      (setcar pair fetchmail-server)
      (setcdr pair passwd)
      (setq fetchmail-passwd-alist
	    (cons pair fetchmail-passwd-alist)))))

(defun fetchmail-get-passwd (fetchmail-server)
  "fetchmail-passwd-alist からパスワードを得る。"
  (cdr (assoc fetchmail-server fetchmail-passwd-alist)))

(defun fetchmail-clear-passwd (fetchmail-server)
  "fetchmail-passwd-alist からパスワードを削除する。"
  (fetchmail-set-passwd fetchmail-server nil))

(defun fetchmail-get-server-param (fetchmail-server fetchmail-param)
  "fetchmail-server-param-alist からサーバのパラメータを得る。"
  (cdr (assq fetchmail-param
	     (cdr (assoc fetchmail-server
			 fetchmail-server-param-alist)))))

(defun fetchmail-param-query-passwd (fetchmail-server query-passwd)
  "サーバのパスワードを設定する。"
  (if query-passwd
      (let ((passwd (fetchmail-get-passwd fetchmail-server)))
	(if (not passwd)
	    (fetchmail-set-passwd fetchmail-server
				  (read-passwd (format "Password for %s: "
						       fetchmail-server)))))))

(defun fetchmail-param-check (fetchmail-server check)
  "check オプションのリストを作る。"
  (if check (list "-c")))

(defun fetchmail-param-username (fetchmail-server username)
  "username オプションのリストを作る。"
  (if username (list "-u" username)))

(defun fetchmail-param-protocol (fetchmail-server protocol)
  "protocol オプションのリストを作る。"
  (if protocol (list "-p" protocol)))

(defun fetchmail-param-port (fetchmail-server port)
  "port オプションのリストを作る。"
  (if port (list "-P" (number-to-string port))))

(defun fetchmail-param-timeout (fetchmail-server timeout)
  "timeout オプションのリストを作る。"
  (if timeout (list "-t" (number-to-string timeout))))

(defun fetchmail-param-folder (fetchmail-server folder)
  "folder オプションのリストを作る。"
  (if folder (list "-r" folder)))

(defun fetchmail-param-keep (fetchmail-server keep)
  "keep オプションのリストを作る。"
  (if keep (list "-k")))

(defun fetchmail-param-flush (fetchmail-server flush)
  "flush オプションのリストを作る。"
  (if flush (list "-F")))

(defun fetchmail-param-funcall (fetchmail-server key value)
  "パラメータに対応する変換関数を呼び出す。"
  (funcall (cdr (assq key fetchmail-param-func-alist))
	   fetchmail-server value))

(defun fetchmail-make-option-list (fetchmail-server fetchmail-param-alist)
  "パラメータから fetchmail のオプションのリストを作る。"
  (if fetchmail-param-alist
      (let ((option-list
	     (fetchmail-param-funcall fetchmail-server
				      (car (car fetchmail-param-alist))
				      (cdr (car fetchmail-param-alist)))))
	(if (cdr fetchmail-param-alist)
	    (let ((next-option-list
		   (fetchmail-make-option-list fetchmail-server
					       (cdr fetchmail-param-alist))))
	      (if option-list
		  (append option-list next-option-list)
		next-option-list))
	  option-list))))

(defun fetchmail-make-server-alist (fetchmail-server-param-alist &optional num)
  "fetchmail サーバの連想リストを作る。"
  (if (not num)
      (setq num 0))
  (if fetchmail-server-param-alist
      (let ((server-node
	     (list (car (car fetchmail-server-param-alist)) num)))
	(if (cdr fetchmail-server-param-alist)
	    (cons server-node
		  (fetchmail-make-server-alist (cdr fetchmail-server-param-alist)
					       (1+ num)))
	  (list server-node)))))

(defun fetchmail-query-server ()
  "fetchmail のサーバをミニバッファで選択する。"
  (completing-read "Fetchmail server: "
		   (fetchmail-make-server-alist fetchmail-server-param-alist)
		   nil t))

(defun fetchmail-make-buffer ()
  "fetchmail バッファを作る。"
  (let ((default-major-mode 'fetchmail-mode))
    (set-buffer-major-mode
     (get-buffer-create fetchmail-buffer-name))))

(defun fetchmail-open-window ()
  "fetchmail バッファをウィンドウで開く。"
  (if (not (equal fetchmail-buffer-name (buffer-name)))
      (set-window-buffer
       (split-window (selected-window) 
		     (- (window-height)
			(max fetchmail-window-height-lower-limit
			     (min fetchmail-window-height-upper-limit
				  (round
				   (* (window-height)
				      fetchmail-window-height-ratio))))
			1))
       fetchmail-buffer-name)))

(defun fetchmail-close-window ()
  "fetchmail バッファのウィンドウを閉じる。"
  (interactive)
  (if (get-buffer-window fetchmail-buffer-name)
      (progn
	(delete-windows-on (get-buffer fetchmail-buffer-name))
	(bury-buffer fetchmail-buffer-name))))

(defun fetchmail-insert-buffer (msg)
  "fetchmail バッファの最後にメッセージを挿入する。"
  (save-excursion
    (set-buffer fetchmail-buffer-name)
    (goto-char (point-max))
    (insert-before-markers msg)))

(defun fetchmail-list-to-string (args)
  "リストを文字列に変換する。"
  (if (cdr args)
      (concat (car args) " "
	      (fetchmail-list-to-string (cdr args)))
    (car args)))

(defun fetchmail-run (fetchmail-server fetchmail-option-list)
  "fetchmail を起動してそのプロセスを返す。"
  (fetchmail-insert-buffer "<<< fetchmail >>>\n")
  (let ((process-connection-type t)
	(fetchmail-run-list (append (list "fetchmail")
				    fetchmail-option-list
				    (list fetchmail-server))))
    (fetchmail-insert-buffer (concat (fetchmail-list-to-string
				      fetchmail-run-list)
				     "\n"))
    (apply 'start-process
	   fetchmail-process-name
	   fetchmail-buffer-name
	   fetchmail-run-list)))

(defun fetchmail-enter-passwd (fetchmail-process passwd)
  "fetchmail プロセスにパスワードを入力する。"
  (catch 'passwd-entered
    (while t
      (save-excursion
	(set-buffer fetchmail-buffer-name)
	(goto-char (process-mark fetchmail-process))
	(beginning-of-line)
	(if (string-match "Enter password"
			  (buffer-substring (point)
					    (process-mark fetchmail-process)))
	    (throw 'passwd-entered nil)))
      (if (not (process-status fetchmail-process))
	  (error "Fetchmail exited in entering password."))
      (sleep-for 0.1)))
  (process-send-string (process-name fetchmail-process)
		       passwd)
  (process-send-eof (process-name fetchmail-process)))

(defun fetchmail-finish (fetchmail-process event)
  "fetchmail プロセス終了時の後始末をする。"
  (let ((fetchmail-exit-status
	 (cond
	  ((string-match "finished" event)
	   'mail)
	  ((string-match "exited" event)
	   (if (= 1 (process-exit-status fetchmail-process))
	       'nomail
	     (fetchmail-clear-passwd fetchmail-last-server)
	     'failure)))))
    (if fetchmail-exit-status
	(progn
	  (setq fetchmail-running nil)
	  (force-mode-line-update)
	  (let ((fetchmail-message
		 (cond
		  ((eq 'mail fetchmail-exit-status)
		   "You have mail.")
		  ((eq 'nomail fetchmail-exit-status)
		   "You have no mail.")
		  ((eq 'failure fetchmail-exit-status)
		   "Failed to fetchmail.")
		  (t
		   (error "Invalid fetchmail-exit-status")))))
	    (run-hooks 'fetchmail-postprocess-hook)
	    (fetchmail-insert-buffer (concat fetchmail-message "\n"))
	    (if (not (get-buffer-window fetchmail-buffer-name))
		(message fetchmail-message))
	    (if fetchmail-notify-beep (beep)))))))

(defun fetchmail-start (fetchmail-server fetchmail-option-list)
  "fetchmail を一つのサーバに対して起動する。"
  (if (get-process fetchmail-process-name)
      (error "Fetchmail is running."))
  (run-hooks 'fetchmail-preprocess-hook)
  (let ((fetchmail-process
	 (fetchmail-run fetchmail-server
			fetchmail-option-list)))
    (if (fetchmail-get-server-param fetchmail-server 'query-passwd)
	(fetchmail-enter-passwd fetchmail-process
				(fetchmail-get-passwd fetchmail-server)))
    (setq fetchmail-running t)
    (force-mode-line-update)
    (setq fetchmail-last-server fetchmail-server)
    (set-process-sentinel fetchmail-process 'fetchmail-finish)))

(defun fetchmail (query-server)
  "fetchmail を起動する。引数を与えるか fetchmail-default-server が
設定されていないときは、ミニバッファで複数のサーバから選択する。"
  (interactive "P")
  (let ((fetchmail-server
	 (cond
	  (query-server
	   (fetchmail-query-server))
	  (fetchmail-default-server
	   fetchmail-default-server)
	  ((= 1 (length fetchmail-server-param-alist))
	   (car (car fetchmail-server-param-alist)))
	  ((= 0 (length fetchmail-server-param-alist))
	   nil)
	  (t
	   (fetchmail-query-server)))))
    (if (not fetchmail-server)
	(error "Not selected fetchmail server."))
    (let ((fetchmail-option-list
	   (fetchmail-make-option-list
	    fetchmail-server
	    (cdr (assoc fetchmail-server
			fetchmail-server-param-alist)))))
      (if (not (get-buffer fetchmail-buffer-name))
	  (fetchmail-make-buffer))
      (if (and fetchmail-window
	       (not (get-buffer-window fetchmail-buffer-name)))
	  (fetchmail-open-window))
      (fetchmail-start fetchmail-server fetchmail-option-list))))

(defun fetchmail-mode ()
  "fetchmail バッファ用のモード。"
  (interactive)
  (setq major-mode 'fetchmail-mode)
  (setq mode-name "Fetchmail")
  (setq fetchmail-mode-map (make-keymap))
  (define-key fetchmail-mode-map "\C-cx" 'fetchmail)
  (define-key fetchmail-mode-map "\C-cq" 'fetchmail-close-window)
  (use-local-map fetchmail-mode-map))
